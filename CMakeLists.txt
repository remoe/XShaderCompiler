
# === CMake lists for the XShaderCompiler - (09/10/2014) ===

cmake_minimum_required(VERSION 2.8)
project(XShaderCompiler)

# === Options ===

# Main
option(XSC_BUILD_SHELL "Build XShaderCompiler shell 'xsc'" OFF)
option(XSC_BUILD_DEBUGGER "Build XShaderCompiler debugger 'xsc_debugger' (requires wxWidgets library)" OFF)
option(XSC_BUILD_TESTS "Build all test applications" OFF)
option(XSC_SHARED_LIB "Build XShaderCompiler as a shared library instead of a static library" OFF)

# Wrappers
option(XSC_BUILD_WRAPPER_C "Build C wrapper (ISO C99)" OFF)
option(XSC_BUILD_WRAPPER_CSHARP "Build C# wrapper" OFF)

# Specials
option(XSC_ENABLE_EASTER_EGGS "Enables little easter eggs" OFF)
option(XSC_ENABLE_POST_VALIDATION "Enables the post validation in presettings with 'glslangValidator'" OFF)
option(XSC_ENABLE_LANGUAGE_EXT "Enables a few language extensions (e.g. 'space' attribute for stronger type system)" ON)
option(XSC_ENABLE_SPIRV "Enables all SPIR-V related features (experimental; requires submodule in 'external/SPIR-V')" OFF)
option(XSC_ENABLE_MOLTENVK_CONVERTER "Enables shader converter for MacOS MoltenVK)" OFF)
option(XSC_ENABLE_DEFAULT_FOLDER_BUILD "Use default folders like bin,lib for build" OFF)
#set(XSC_REPORT_LANGUAGE "EN" CACHE STRING "Language of the report output (supported values: 'EN')")


# === Build path ===
if(!XSC_ENABLE_DEFAULT_FOLDER_BUILD)
	set(BUILD_OUTPUT_PATH "${CMAKE_CURRENT_BINARY_DIR}/build")
	set(EXECUTABLE_OUTPUT_PATH ${BUILD_OUTPUT_PATH} CACHE PATH "Build directory" FORCE)
	set(LIBRARY_OUTPUT_PATH ${BUILD_OUTPUT_PATH} CACHE PATH "Build directory" FORCE)
	set(INSTALL_OUTPUT_PATH "${CMAKE_CURRENT_BINARY_DIR}/install" CACHE PATH "Installation directory")
endif()


# === Preprocessor definitions ===

add_definitions(-D_CRT_SECURE_NO_WARNINGS)

if(XSC_SHARED_LIB)
	add_definitions(-DXSC_SHARED_LIB)
endif()

if(XSC_ENABLE_EASTER_EGGS)
	add_definitions(-DXSC_ENABLE_EASTER_EGGS)
endif()

if(XSC_ENABLE_POST_VALIDATION)
	add_definitions(-DXSC_ENABLE_POST_VALIDATION)
endif()

if(XSC_ENABLE_LANGUAGE_EXT)
    add_definitions(-DXSC_ENABLE_LANGUAGE_EXT)
endif()

if(XSC_ENABLE_SPIRV)
	add_definitions(-DXSC_ENABLE_SPIRV)
endif()

if(XSC_ENABLE_MOLTENVK_CONVERTER)
	add_definitions(-DXSC_ENABLE_MOLTENVK_CONVERTER)
endif()

# === Global files ===

file(GLOB FilesInc ${PROJECT_SOURCE_DIR}/inc/Xsc/*.*)
file(GLOB FilesIncC ${PROJECT_SOURCE_DIR}/inc/XscC/*.*)
file(GLOB FilesSrcCompiler ${PROJECT_SOURCE_DIR}/src/Compiler/*.*)
file(GLOB FilesSrcCompilerAST ${PROJECT_SOURCE_DIR}/src/Compiler/AST/*.*)
file(GLOB FilesSrcCompilerASTVisitor ${PROJECT_SOURCE_DIR}/src/Compiler/AST/Visitor/*.*)
file(GLOB FilesSrcCompilerFrontend ${PROJECT_SOURCE_DIR}/src/Compiler/Frontend/*.*)
file(GLOB FilesSrcCompilerFrontendHLSL ${PROJECT_SOURCE_DIR}/src/Compiler/Frontend/HLSL/*.*)
file(GLOB FilesSrcCompilerFrontendGLSL ${PROJECT_SOURCE_DIR}/src/Compiler/Frontend/GLSL/*.*)
file(GLOB FilesSrcCompilerBackend ${PROJECT_SOURCE_DIR}/src/Compiler/Backend/*.*)
file(GLOB FilesSrcCompilerBackendGLSL ${PROJECT_SOURCE_DIR}/src/Compiler/Backend/GLSL/*.*)
file(GLOB FilesSrcCompilerReport ${PROJECT_SOURCE_DIR}/src/Compiler/Report/*.*)
file(GLOB FilesSrcCompilerCFG ${PROJECT_SOURCE_DIR}/src/Compiler/CFG/*.*)
file(GLOB FilesSrcShell ${PROJECT_SOURCE_DIR}/src/Shell/*.*)
file(GLOB FilesSrcDebugger ${PROJECT_SOURCE_DIR}/src/Debugger/*.*)
file(GLOB FilesSrcWrapperC ${PROJECT_SOURCE_DIR}/src/Wrapper/C/*.*)
#file(GLOB FilesSrcWrapperCSharp ${PROJECT_SOURCE_DIR}/src/Wrapper/CSharp/*.*)

set(FilesTest ${PROJECT_SOURCE_DIR}/test)

if(WIN32)
	file(GLOB FilesSrcCompilerPlatform ${PROJECT_SOURCE_DIR}/src/Compiler/Platform/Win32/*.*)
else()
	file(GLOB FilesSrcCompilerPlatform ${PROJECT_SOURCE_DIR}/src/Compiler/Platform/Unix/*.*)
endif()

set(
	FilesCompilerAll
	${FilesInc}
	${FilesSrcCompiler}
	${FilesSrcCompilerAST}
	${FilesSrcCompilerASTVisitor}
	${FilesSrcCompilerPlatform}
	${FilesSrcCompilerFrontend}
	${FilesSrcCompilerFrontendHLSL}
	${FilesSrcCompilerFrontendGLSL}
	${FilesSrcCompilerBackend}
	${FilesSrcCompilerBackendGLSL}
	${FilesSrcCompilerReport}
)

if(XSC_ENABLE_SPIRV)
	set(FilesCompilerAll ${FilesCompilerAll} ${FilesSrcCompilerCFG})
endif()


# === Source group folders ===

source_group("inc\\Xsc" FILES ${FilesInc})
source_group("inc\\XscC" FILES ${FilesIncC})
source_group("src" FILES ${FilesSrcCompiler} ${FilesSrcShell} ${FilesSrcDebugger})
source_group("src\\AST" FILES ${FilesSrcCompilerAST})
source_group("src\\AST\\Visitor" FILES ${FilesSrcCompilerASTVisitor})
source_group("src\\Platform" FILES ${FilesSrcCompilerPlatform})
source_group("src\\Frontend" FILES ${FilesSrcCompilerFrontend})
source_group("src\\Frontend\\HLSL" FILES ${FilesSrcCompilerFrontendHLSL})
source_group("src\\Frontend\\GLSL" FILES ${FilesSrcCompilerFrontendGLSL})
source_group("src\\Backend" FILES ${FilesSrcCompilerBackend})
source_group("src\\Backend\\GLSL" FILES ${FilesSrcCompilerBackendGLSL})
source_group("src\\Report" FILES ${FilesSrcCompilerReport})

if(XSC_ENABLE_SPIRV)
	source_group("src\\CFG" FILES ${FilesSrcCompilerCFG})
endif()


# === Include directories ===

include_directories("${PROJECT_SOURCE_DIR}/inc")

include_directories("${PROJECT_SOURCE_DIR}/src/Compiler")
include_directories("${PROJECT_SOURCE_DIR}/src/Compiler/AST")
include_directories("${PROJECT_SOURCE_DIR}/src/Compiler/AST/Visitor")
include_directories("${PROJECT_SOURCE_DIR}/src/Compiler/Frontend")
include_directories("${PROJECT_SOURCE_DIR}/src/Compiler/Frontend/HLSL")
include_directories("${PROJECT_SOURCE_DIR}/src/Compiler/Frontend/GLSL")
include_directories("${PROJECT_SOURCE_DIR}/src/Compiler/Backend")
include_directories("${PROJECT_SOURCE_DIR}/src/Compiler/Backend/GLSL")
include_directories("${PROJECT_SOURCE_DIR}/src/Compiler/Report")

if(XSC_ENABLE_SPIRV)
	include_directories("${PROJECT_SOURCE_DIR}/src/Compiler/CFG")
	include_directories("${PROJECT_SOURCE_DIR}/external/SPIR-V/include")
endif()

# === Compile flags ===
if(CMAKE_CXX_COMPILER_ID MATCHES "Clang" OR CMAKE_CXX_COMPILER_ID MATCHES "GNU")
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fPIC")
endif()

if(MSVC)
	set(CMAKE_CXX_FLAGS_RELWITHDEBINFO "${CMAKE_CXX_FLAGS_RELWITHDEBINFO} /MDd")
endif()

# === Executable ===

# Library core
if(XSC_SHARED_LIB)
	add_library(xsc_core SHARED ${FilesCompilerAll})
else()
	add_library(xsc_core STATIC ${FilesCompilerAll})
endif()

set_target_properties(xsc_core PROPERTIES LINKER_LANGUAGE CXX)
target_compile_features(xsc_core PRIVATE cxx_range_for)

set(XSC_INSTALL_TARGETS "xsc_core")

# Shell application
if(XSC_BUILD_SHELL)
	add_executable(xsc ${FilesSrcShell})
	set_target_properties(xsc PROPERTIES LINKER_LANGUAGE CXX)
	target_link_libraries(xsc xsc_core)
	target_compile_features(xsc PRIVATE cxx_range_for)
	set(XSC_INSTALL_TARGETS ${XSC_INSTALL_TARGETS} "xsc")
endif()

# Debugger UI aplication
if(XSC_BUILD_DEBUGGER)
	if(WIN32)
		add_executable(xsc_debugger WIN32 ${FilesSrcDebugger} "${PROJECT_SOURCE_DIR}/src/Debugger/Resources.rc")
	else()
		add_executable(xsc_debugger ${FilesSrcDebugger})
	endif()
	
	set_target_properties(xsc_debugger PROPERTIES LINKER_LANGUAGE CXX)
	target_link_libraries(xsc_debugger xsc_core)
	target_compile_features(xsc_debugger PRIVATE cxx_range_for)
	
	# wxWidgets Libs
	find_package(wxWidgets REQUIRED adv aui base core stc propgrid richtext html xml scintilla)
	if(wxWidgets_FOUND)
		message("Found wxWidgets library")
		include_directories(${wxWidgets_INCLUDE_DIRS})
		target_link_libraries(xsc_debugger ${wxWidgets_LIBRARIES})
	else(wxWidgets_FOUND)
		message(SEND_ERROR "Missing wxWidgets library")
	endif(wxWidgets_FOUND)
	
	set(XSC_INSTALL_TARGETS ${XSC_INSTALL_TARGETS} "xsc_debugger")
endif()

# C wrapper
if(XSC_BUILD_WRAPPER_C)
	if(XSC_SHARED_LIB)
		add_library(xsc_core_c SHARED ${FilesSrcWrapperC})
	else()
		add_library(xsc_core_c STATIC ${FilesSrcWrapperC})
	endif()
	
	set_target_properties(xsc_core_c PROPERTIES LINKER_LANGUAGE CXX)
	target_link_libraries(xsc_core_c xsc_core)
	target_compile_features(xsc_core_c PRIVATE cxx_range_for)
	
	set(XSC_INSTALL_TARGETS ${XSC_INSTALL_TARGETS} "xsc_core_c")
endif()

# C# wrapper
if(WIN32 AND XSC_BUILD_WRAPPER_CSHARP)
	add_subdirectory(src/Wrapper/CSharp)
endif()

if(XSC_BUILD_TESTS)
	# Test C wrapper
	if(XSC_BUILD_WRAPPER_C)
		add_executable(XscTest_CWrapper "${FilesTest}/XscTest_CWrapper.c")
		set_target_properties(XscTest_CWrapper PROPERTIES LINKER_LANGUAGE C)
		target_link_libraries(XscTest_CWrapper xsc_core_c)
	endif()
endif()


# === Show summary ===

message("~~~ Build Summary ~~~")

if(XSC_SHARED_LIB)
	message("Build Core: xsc_core (SHARED)")
else()
	message("Build Core: xsc_core (STATIC)")
endif()

if(XSC_BUILD_SHELL)
	message("Build Shell: xsc")
endif()

if(XSC_BUILD_DEBUGGER)
	message("Build Debugger: xsc_debugger")
endif()

if(XSC_BUILD_WRAPPER_C)
	message("Build C Wrapper: xsc_core_c")
endif()

if(XSC_BUILD_WRAPPER_CSHARP)
	message("Build C# Wrapper: xsc_core_csharp")
endif()

if(XSC_ENABLE_SPIRV)
	message("Include SPIR-V (Experimental)")
endif()


# === Installation Rules ===

if(XSC_ENABLE_DEFAULT_FOLDER_BUILD)
	install(
		TARGETS ${XSC_INSTALL_TARGETS}
		RUNTIME DESTINATION bin
		LIBRARY DESTINATION lib
		ARCHIVE DESTINATION lib
	)
	install(
		DIRECTORY inc/Xsc
		DESTINATION include
	)
else()
	install(
		TARGETS ${XSC_INSTALL_TARGETS}
		DESTINATION ${INSTALL_OUTPUT_PATH}
	)
	install(
		DIRECTORY inc/Xsc
		DESTINATION ${INSTALL_OUTPUT_PATH}/include
	)
	IF(MSVC)
		install(
			FILES ${CMAKE_BINARY_DIR}/xsc_core.dir/RelWithDebInfo/xsc_core.pdb 
			DESTINATION ${INSTALL_OUTPUT_PATH} 
			CONFIGURATIONS RelWithDebInfo
		)

		install(
			FILES ${CMAKE_BINARY_DIR}/xsc_core.dir/Debug/xsc_core.pdb 
			DESTINATION ${INSTALL_OUTPUT_PATH} 
			CONFIGURATIONS Debug
		)	
	ENDIF(MSVC)		
endif()

